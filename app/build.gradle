plugins {
    id 'com.android.application'
}

def getAppid() {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    String result = properties.getProperty("pgy_appid")
    return result == null ? "" : result
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.razerdp.huobi_analysis"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 88
        versionName "3.0"

        renderscriptTargetApi 18
        renderscriptSupportModeEnabled true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        rxhttp_rxjava : 'rxjava2',
                        rxhttp_package: 'rxhttp'
                ]
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            manifestPlaceholders = [APPID: getAppid(),
                                    name : "(Debug)Huobi-Analysis"]
        }
        release {
            minifyEnabled false
            manifestPlaceholders = [APPID: getAppid(),
                                    name : "Huobi-Analysis"]
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dexOptions {
        javaMaxHeapSize "4g"
        preDexLibraries = true
        maxProcessCount = 4
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.recyclerview:recyclerview:1.2.0'
    implementation 'com.github.razerdp:StrokeLayouts:1.0.0'
    //net
    implementation 'com.squareup.okhttp3:okhttp:4.9.1'
    implementation 'com.squareup.okhttp3:okhttp-urlconnection:3.10.0'
    //gson
    implementation 'com.google.code.gson:gson:2.8.6'
    //懒得封装webview而用的库
    implementation 'com.github.Justson.AgentWeb:agentweb-core:v4.1.9-androidx'
    // rx
    implementation 'io.reactivex.rxjava2:rxjava:2.2.14'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    // permission
    implementation 'pub.devrel:easypermissions:3.0.0'
    // basepopup
    implementation 'io.github.razerdp:BasePopup:3.1.8'
    // rxhttp
    implementation 'com.github.liujingxing.rxhttp:rxhttp:2.6.1'
    implementation 'com.github.liujingxing.rxlife:rxlife-rxjava2:2.1.0'
    annotationProcessor 'com.github.liujingxing.rxhttp:rxhttp-compiler:2.6.1'
    // butterknife
    implementation "com.jakewharton:butterknife:10.2.1"
    annotationProcessor "com.jakewharton:butterknife-compiler:10.2.1"
    // time
    implementation "org.threeten:threetenbp:1.5.1"
    // 蒲公英
    implementation 'com.pgyersdk:sdk:3.0.10'
}